/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import topology.basic
import order.filter.small_sets

/-!
### Locally finite families of sets

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

We say that a family of sets in a topological space is *locally finite* if at every point `x : X`,
there is a neighborhood of `x` which meets only finitely many sets in the family.

In this file we give the definition and prove basic properties of locally finite families of sets.
-/

/- locally finite family [General Topology (Bourbaki, 1995)] -/

open set function filter
open_locale topology filter

universe u
variables {Œπ : Type u} {Œπ' Œ± X Y : Type*} [topological_space X] [topological_space Y]
  {f g : Œπ ‚Üí set X}

/-- A family of sets in `set X` is locally finite if at every point `x : X`,
there is a neighborhood of `x` which meets only finitely many sets in the family. -/
def locally_finite (f : Œπ ‚Üí set X) :=
‚àÄ x : X, ‚àÉt ‚àà ùìù x, {i | (f i ‚à© t).nonempty}.finite

lemma locally_finite_of_finite [finite Œπ] (f : Œπ ‚Üí set X) : locally_finite f :=
assume x, ‚ü®univ, univ_mem, to_finite _‚ü©

namespace locally_finite

lemma point_finite (hf : locally_finite f) (x : X) : {b | x ‚àà f b}.finite :=
let ‚ü®t, hxt, ht‚ü© := hf x in ht.subset $ Œª b hb, ‚ü®x, hb, mem_of_mem_nhds hxt‚ü©

protected lemma subset (hf : locally_finite f) (hg : ‚àÄ i, g i ‚äÜ f i) : locally_finite g :=
assume a,
let ‚ü®t, ht‚ÇÅ, ht‚ÇÇ‚ü© := hf a in
‚ü®t, ht‚ÇÅ, ht‚ÇÇ.subset $ assume i hi, hi.mono $ inter_subset_inter (hg i) subset.rfl‚ü©

lemma comp_inj_on {g : Œπ' ‚Üí Œπ} (hf : locally_finite f)
  (hg : inj_on g {i | (f (g i)).nonempty}) : locally_finite (f ‚àò g) :=
Œª x, let ‚ü®t, htx, htf‚ü© := hf x in ‚ü®t, htx, htf.preimage $ hg.mono $ Œª i hi,
  hi.out.mono $ inter_subset_left _ _‚ü©

lemma comp_injective {g : Œπ' ‚Üí Œπ} (hf : locally_finite f) (hg : injective g) :
  locally_finite (f ‚àò g) :=
hf.comp_inj_on (hg.inj_on _)

lemma _root_.locally_finite_iff_small_sets :
  locally_finite f ‚Üî ‚àÄ x, ‚àÄ·∂† s in (ùìù x).small_sets, {i | (f i ‚à© s).nonempty}.finite :=
forall_congr $ Œª x, iff.symm $ eventually_small_sets' $ Œª s t hst ht, ht.subset $
  Œª i hi, hi.mono $ inter_subset_inter_right _ hst

protected lemma eventually_small_sets (hf : locally_finite f) (x : X) :
  ‚àÄ·∂† s in (ùìù x).small_sets, {i | (f i ‚à© s).nonempty}.finite :=
locally_finite_iff_small_sets.mp hf x

lemma exists_mem_basis {Œπ' : Sort*} (hf : locally_finite f) {p : Œπ' ‚Üí Prop}
  {s : Œπ' ‚Üí set X} {x : X} (hb : (ùìù x).has_basis p s) :
  ‚àÉ i (hi : p i), {j | (f j ‚à© s i).nonempty}.finite :=
let ‚ü®i, hpi, hi‚ü© := hb.small_sets.eventually_iff.mp (hf.eventually_small_sets x)
in ‚ü®i, hpi, hi subset.rfl‚ü©

protected lemma closure (hf : locally_finite f) : locally_finite (Œª i, closure (f i)) :=
begin
  intro x,
  rcases hf x with ‚ü®s, hsx, hsf‚ü©,
  refine ‚ü®interior s, interior_mem_nhds.2 hsx, hsf.subset $ Œª i hi, _‚ü©,
  exact (hi.mono is_open_interior.closure_inter).of_closure.mono
    (inter_subset_inter_right _ interior_subset)
end

lemma is_closed_Union (hf : locally_finite f) (hc : ‚àÄi, is_closed (f i)) :
  is_closed (‚ãÉi, f i) :=
begin
  simp only [‚Üê is_open_compl_iff, compl_Union, is_open_iff_mem_nhds, mem_Inter],
  intros a ha,
  replace ha : ‚àÄ i, (f i)·∂ú ‚àà ùìù a := Œª i, (hc i).is_open_compl.mem_nhds (ha i),
  rcases hf a with ‚ü®t, h_nhds, h_fin‚ü©,
  have : t ‚à© (‚ãÇ i ‚àà {i | (f i ‚à© t).nonempty}, (f i)·∂ú) ‚àà ùìù a,
    from inter_mem h_nhds ((bInter_mem h_fin).2 (Œª i _, ha i)),
  filter_upwards [this],
  simp only [mem_inter_iff, mem_Inter],
  rintros b ‚ü®hbt, hn‚ü© i hfb,
  exact hn i ‚ü®b, hfb, hbt‚ü© hfb,
end

lemma closure_Union (h : locally_finite f) : closure (‚ãÉ i, f i) = ‚ãÉ i, closure (f i) :=
subset.antisymm
  (closure_minimal (Union_mono $ Œª _, subset_closure) $
    h.closure.is_closed_Union $ Œª _, is_closed_closure)
  (Union_subset $ Œª i, closure_mono $ subset_Union _ _)

/-- If `f : Œ≤ ‚Üí set Œ±` is a locally finite family of closed sets, then for any `x : Œ±`, the
intersection of the complements to `f i`, `x ‚àâ f i`, is a neighbourhood of `x`. -/
lemma Inter_compl_mem_nhds (hf : locally_finite f) (hc : ‚àÄ i, is_closed (f i)) (x : X) :
  (‚ãÇ i (hi : x ‚àâ f i), (f i)·∂ú) ‚àà ùìù x :=
begin
  refine is_open.mem_nhds _ (mem_Inter‚ÇÇ.2 $ Œª i, id),
  suffices : is_closed (‚ãÉ i : {i // x ‚àâ f i}, f i),
    by rwa [‚Üê is_open_compl_iff, compl_Union, Inter_subtype] at this,
  exact (hf.comp_injective subtype.coe_injective).is_closed_Union (Œª i, hc _)
end

/-- Let `f : ‚Ñï ‚Üí Œ† a, Œ≤ a` be a sequence of (dependent) functions on a topological space. Suppose
that the family of sets `s n = {x | f (n + 1) x ‚â† f n x}` is locally finite. Then there exists a
function `F : Œ† a, Œ≤ a` such that for any `x`, we have `f n x = F x` on the product of an infinite
interval `[N, +‚àû)` and a neighbourhood of `x`.

We formulate the conclusion in terms of the product of filter `filter.at_top` and `ùìù x`. -/
lemma exists_forall_eventually_eq_prod {œÄ : X ‚Üí Sort*} {f : ‚Ñï ‚Üí Œ† x : X, œÄ x}
  (hf : locally_finite (Œª n, {x | f (n + 1) x ‚â† f n x})) :
  ‚àÉ F : Œ† x : X, œÄ x, ‚àÄ x, ‚àÄ·∂† p : ‚Ñï √ó X in at_top √ó·∂† ùìù x, f p.1 p.2 = F p.2 :=
begin
  choose U hUx hU using hf,
  choose N hN using Œª x, (hU x).bdd_above,
  replace hN : ‚àÄ x (n > N x) (y ‚àà U x), f (n + 1) y = f n y,
    from Œª x n hn y hy, by_contra (Œª hne, hn.lt.not_le $ hN x ‚ü®y, hne, hy‚ü©),
  replace hN : ‚àÄ x (n ‚â• N x + 1) (y ‚àà U x), f n y = f (N x + 1) y,
    from Œª x n hn y hy, nat.le_induction rfl (Œª k hle, (hN x _ hle _ hy).trans) n hn,
  refine ‚ü®Œª x, f (N x + 1) x, Œª x, _‚ü©,
  filter_upwards [filter.prod_mem_prod (eventually_gt_at_top (N x)) (hUx x)],
  rintro ‚ü®n, y‚ü© ‚ü®hn : N x < n, hy : y ‚àà U x‚ü©,
  calc f n y = f (N x + 1) y : hN _ _ hn _ hy
  ... = f (max (N x + 1) (N y + 1)) y : (hN _ _ (le_max_left _ _) _ hy).symm
  ... = f (N y + 1) y : hN _ _ (le_max_right _ _) _ (mem_of_mem_nhds $ hUx y)
end

/-- Let `f : ‚Ñï ‚Üí Œ† a, Œ≤ a` be a sequence of (dependent) functions on a topological space. Suppose
that the family of sets `s n = {x | f (n + 1) x ‚â† f n x}` is locally finite. Then there exists a
function `F : Œ† a, Œ≤ a` such that for any `x`, for sufficiently large values of `n`, we have
`f n y = F y` in a neighbourhood of `x`. -/
lemma exists_forall_eventually_at_top_eventually_eq' {œÄ : X ‚Üí Sort*}
  {f : ‚Ñï ‚Üí Œ† x : X, œÄ x} (hf : locally_finite (Œª n, {x | f (n + 1) x ‚â† f n x})) :
  ‚àÉ F : Œ† x : X, œÄ x, ‚àÄ x, ‚àÄ·∂† n : ‚Ñï in at_top, ‚àÄ·∂† y : X in ùìù x, f n y = F y :=
hf.exists_forall_eventually_eq_prod.imp $ Œª F hF x, (hF x).curry

/-- Let `f : ‚Ñï ‚Üí Œ± ‚Üí Œ≤` be a sequence of functions on a topological space. Suppose
that the family of sets `s n = {x | f (n + 1) x ‚â† f n x}` is locally finite. Then there exists a
function `F :  Œ± ‚Üí Œ≤` such that for any `x`, for sufficiently large values of `n`, we have
`f n =·∂†[ùìù x] F`. -/
lemma exists_forall_eventually_at_top_eventually_eq {f : ‚Ñï ‚Üí X ‚Üí Œ±}
  (hf : locally_finite (Œª n, {x | f (n + 1) x ‚â† f n x})) :
  ‚àÉ F : X ‚Üí Œ±, ‚àÄ x, ‚àÄ·∂† n : ‚Ñï in at_top, f n =·∂†[ùìù x] F :=
hf.exists_forall_eventually_at_top_eventually_eq'

lemma preimage_continuous {g : Y ‚Üí X} (hf : locally_finite f) (hg : continuous g) :
  locally_finite (Œª i, g ‚Åª¬π' (f i)) :=
Œª x, let ‚ü®s, hsx, hs‚ü© := hf (g x)
  in ‚ü®g ‚Åª¬π' s, hg.continuous_at hsx, hs.subset $ Œª i ‚ü®y, hy‚ü©, ‚ü®g y, hy‚ü©‚ü©

end locally_finite

@[simp] lemma equiv.locally_finite_comp_iff (e : Œπ' ‚âÉ Œπ) :
  locally_finite (f ‚àò e) ‚Üî locally_finite f :=
‚ü®Œª h, by simpa only [(‚àò), e.apply_symm_apply] using h.comp_injective e.symm.injective,
  Œª h, h.comp_injective e.injective‚ü©

lemma locally_finite_sum {f : Œπ ‚äï Œπ' ‚Üí set X} :
  locally_finite f ‚Üî locally_finite (f ‚àò sum.inl) ‚àß locally_finite (f ‚àò sum.inr) :=
by simp only [locally_finite_iff_small_sets, ‚Üê forall_and_distrib, ‚Üê finite_preimage_inl_and_inr,
  preimage_set_of_eq, (‚àò), eventually_and]

lemma locally_finite.sum_elim {g : Œπ' ‚Üí set X} (hf : locally_finite f) (hg : locally_finite g) :
  locally_finite (sum.elim f g) :=
locally_finite_sum.mpr ‚ü®hf, hg‚ü©

lemma locally_finite_option {f : option Œπ ‚Üí set X} :
  locally_finite f ‚Üî locally_finite (f ‚àò some) :=
begin
  simp only [‚Üê (equiv.option_equiv_sum_punit.{u} Œπ).symm.locally_finite_comp_iff,
    locally_finite_sum, locally_finite_of_finite, and_true],
  refl
end

lemma locally_finite.option_elim (hf : locally_finite f) (s : set X) :
  locally_finite (option.elim s f) :=
locally_finite_option.2 hf
